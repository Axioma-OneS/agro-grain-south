
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОткрытаВКонтекстеЗакрытияМесяца = Параметры.Свойство("Период") И Параметры.Свойство("МассивОрганизаций");
	
	Если Параметры.Свойство("ИзмененияЗапрещены") Тогда
		ИзмененияЗапрещены = Параметры.ИзмененияЗапрещены;
	Иначе
		ИзмененияЗапрещены = Ложь;
	КонецЕсли;
	
	Если ОткрытаВКонтекстеЗакрытияМесяца Тогда
		
		Период = НачалоМесяца(Параметры.Период);
	
		СписокОрганизаций.ЗагрузитьЗначения(Параметры.МассивОрганизаций);
		
	Иначе
		
		Период = НачалоМесяца(ТекущаяДатаСеанса());
	
		СписокОрганизаций.ЗагрузитьЗначения(Справочники.Организации.ДоступныеОрганизации(Истина));
		
	КонецЕсли;
	
	ПредставлениеПериода = ОбщегоНазначенияУТКлиентСервер.ПолучитьПредставлениеПериодаРегистрации(Период);
	
	Элементы.ПредставлениеПериода.ТолькоПросмотр = ОткрытаВКонтекстеЗакрытияМесяца;
	Элементы.СписокОрганизаций.Видимость = НЕ ОткрытаВКонтекстеЗакрытияМесяца;
	Элементы.РегистраторыДляПерепроведения.Видимость = Ложь;
	
	Элементы.ФормаИгнорировать.Видимость = ОткрытаВКонтекстеЗакрытияМесяца;
	Элементы.ФормаПодтвердитьВыполнение.Видимость = Ложь;
	Элементы.ФормаЗакрытьФорму.Видимость = Ложь;
	
	Элементы.ТекстДляИнформации.Видимость = ОткрытаВКонтекстеЗакрытияМесяца;
	
	Если ОткрытаВКонтекстеЗакрытияМесяца Тогда
		
		Если Константы.ЗагружатьИУстанавливатьИсправленияАвтоматически.Получить() Тогда
			
			ТекстДляИнформации = НСтр("ru='В вашей информационной базе используется автоматическая установка исправлений (патчей).
				|В результате ошибки 00-00303344 в патче EF_00_00298005 возможны ошибки в движениях документов за указанный выше период.
				|Рекомендуется выполнить групповое проведение документов за этот период. Проведение документов может занять продолжительное время.
				|После проведения документов необходимо обновить форму операций закрытия месяца и повторно выполнить операции закрытия месяца.'");
			
		Иначе
			
			ТекстДляИнформации = НСтр("ru='В вашей информационной базе отключена автоматическая установка исправлений (патчей).
				|Уточните у администратора вашей информационной базы, не устанавливался ли патч EF_00_00298005 вручную.
				|Если патч не устанавливался, то нажмите кнопку ""Игнорировать"" в шапке формы.
				|
				|Если патч устанавливался, то в результате ошибки 00-00303344 в вашей информационной базе возможны ошибки в движениях документов за указанный выше период.
				|Рекомендуется выполнить групповое проведение документов за этот период. Проведение документов может занять продолжительное время.
				|После проведения документов необходимо обновить форму операций закрытия месяца и повторно выполнить операции закрытия месяца.'");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИзмененияЗапрещены Тогда
		
		Элементы.ФормаПерепровестиДокументы.Доступность = Ложь;
		Элементы.ФормаИгнорировать.Доступность = Ложь;
		Элементы.ФормаПодтвердитьВыполнение.Доступность = Ложь;
		
		Элементы.ФормаПерепровестиДокументы.Видимость = Ложь;
		Элементы.ФормаИгнорировать.Видимость = Ложь;
		Элементы.ФормаЗакрытьФорму.Видимость = Истина;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Изменения в данном периоде запрещены. Подробнее см. в форме операций закрытия месяца.'"));
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ПредставлениеПериодаНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыФормы 	   = Новый Структура("Значение, РежимВыбораПериода", Период, "МЕСЯЦ");
	
	ОткрытьФорму("ОбщаяФорма.ВыборПериода",
		ПараметрыФормы, 
		ЭтотОбъект, 
		УникальныйИдентификатор,
		,
		, 
		ОбработчикЗакрытия,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ОбщегоНазначенияУТКлиент.РегулированиеПредставленияПериодаРегистрации(
		Направление,
		СтандартнаяОбработка,
		Период,
		ПредставлениеПериода);
	
КонецПроцедуры


&НаКлиенте
Процедура РегистраторыДляПерепроведенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, РегистраторыДляПерепроведения.Получить(ВыбраннаяСтрока).Регистратор);
	
КонецПроцедуры


&НаКлиенте
Процедура СброситьРезультатыВыполнения(Команда)
	СброситьРезультатыВыполненияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьВыполнение(Команда)
	
	ПодтвердитьВыполнениеНаСервере();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Игнорировать(Команда)
	
	ИгнорироватьНаСервере();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерепровестиДокументы(Команда)
	
	ПерепровестиДокументыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПерепровестиДокументыНаСервере()
	
	РегистраторыСОшибками = Обработки.ПерепроведениеДокументовПоРегистрамСебестоимости.ПерепровестиДокументы(
		Период,
		СписокОрганизаций.ВыгрузитьЗначения(),
		ОткрытаВКонтекстеЗакрытияМесяца);
	
	РегистраторыДляПерепроведения.Очистить();
	
	Если РегистраторыСОшибками.Количество() = 0 Тогда
		
		// Все документы перепроведены успешно
		ВыполненоУспешно = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Проведение документов выполнено успешно.'"));
		
		Если ОткрытаВКонтекстеЗакрытияМесяца Тогда
			
			Обработки.ПерепроведениеДокументовПоРегистрамСебестоимости.СохранитьРезультатыОбработки(
				Период,
				СписокОрганизаций.ВыгрузитьЗначения(),
				Ложь,
				Истина);
			
		КонецЕсли;
			
	Иначе
		
		// Есть не проведенные документы
		
		ТекстДляЖурнала = "";
		ВыполненоУспешно = Ложь;
		
		Для Каждого ТекущаяСтрока Из РегистраторыСОшибками Цикл
			
			ЗаполнитьЗначенияСвойств(РегистраторыДляПерепроведения.Добавить(), ТекущаяСтрока);
			
			ТекстДляЖурнала = ТекстДляЖурнала + Символы.ПС + СокрЛП(ТекущаяСтрока.Регистратор);
			
		КонецЦикла;
		
		ТекстДляЖурнала = НСтр("ru='Регистраторы для перепроведения вручную'") + ":" + Символы.ПС + СокрЛП(ТекстДляЖурнала); 
		
		ЗаписьЖурналаРегистрации(
			Обработки.ПерепроведениеДокументовПоРегистрамСебестоимости.ИмяСобытияДляЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстДляЖурнала);
			
		Если ОткрытаВКонтекстеЗакрытияМесяца Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Проведение документов выполнено с ошибками. Указанные документы необходимо провести вручную.
				|После окончания проведения нажмите кнопку ""Подтвердить выполнение"".'"));
			
			Обработки.ПерепроведениеДокументовПоРегистрамСебестоимости.СохранитьРезультатыОбработки(
				Период,
				СписокОрганизаций.ВыгрузитьЗначения(),
				Ложь,
				Ложь,
				РегистраторыСОшибками);
			
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Проведение документов выполнено с ошибками.'"));
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.РегистраторыДляПерепроведения.Видимость = НЕ ВыполненоУспешно;
	
	Элементы.ФормаИгнорировать.Видимость = Ложь;
	Элементы.ФормаПодтвердитьВыполнение.Видимость = ОткрытаВКонтекстеЗакрытияМесяца И НЕ ВыполненоУспешно;
	
	Элементы.ФормаПерепровестиДокументы.Видимость = НЕ ОткрытаВКонтекстеЗакрытияМесяца ИЛИ НЕ ВыполненоУспешно;
	Элементы.ФормаЗакрытьФорму.Видимость = ОткрытаВКонтекстеЗакрытияМесяца И ВыполненоУспешно;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбораЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт 
	
	Если ВыбранныйПериод <> Неопределено Тогда
		
		Период = ВыбранныйПериод;
		ПредставлениеПериода = ОбщегоНазначенияУТКлиентСервер.ПолучитьПредставлениеПериодаРегистрации(Период);
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура СброситьРезультатыВыполненияНаСервере()
	
	Обработки.ПерепроведениеДокументовПоРегистрамСебестоимости.ОчиститьРезультатыОбработки();
	
КонецПроцедуры

&НаСервере
Процедура ПодтвердитьВыполнениеНаСервере()
	
	Обработки.ПерепроведениеДокументовПоРегистрамСебестоимости.СохранитьРезультатыОбработки(Период, СписокОрганизаций.ВыгрузитьЗначения(), Ложь, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ИгнорироватьНаСервере()
	
	Обработки.ПерепроведениеДокументовПоРегистрамСебестоимости.СохранитьРезультатыОбработки(Период, СписокОрганизаций.ВыгрузитьЗначения(), Истина, Ложь);
	
КонецПроцедуры

#КонецОбласти
